plugins {
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'jacoco'
	id 'java'
	id 'com.diffplug.spotless' version '6.22.0'
	id "io.freefair.lombok" version "8.4"
}

group = 'org.miracum.streams.ume'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2022.0.4")
	set('hapiVersion', "6.8.3")
}

dependencies {
	implementation 'org.apache.kafka:kafka-streams'
	implementation 'org.springframework.cloud:spring-cloud-stream'
	implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka-streams'
	implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:${hapiVersion}"
	implementation 'io.micrometer:micrometer-registry-prometheus:1.11.5'
	implementation 'io.micrometer:micrometer-core:1.11.5'
	implementation "org.miracum:kafka-fhir-serializer:1.0.4"
	implementation "org.apache.commons:commons-text:1.10.0"
	implementation "org.scala-lang:scala-library:2.13.12"

	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	testImplementation "ca.uhn.hapi.fhir:hapi-fhir-validation:${hapiVersion}"
	testImplementation "ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:${hapiVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.cloud:spring-cloud-stream-test-support"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

jacoco {
	toolVersion = "0.8.10"
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
		googleJavaFormat()
	}
}

jacocoTestReport {
	reports {
		csv.outputLocation = file(layout.buildDirectory.dir('jacoco/coverage.csv'))
		csv.required = true
		html.outputLocation = file(layout.buildDirectory.dir('jacoco/coverage.html'))
		html.required = true
		xml.required = false
	}
}

jar {
	enabled = false
}
