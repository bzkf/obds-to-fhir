// groovylint-disable NestedBlockDepth

plugins {
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'java'
    id 'com.diffplug.spotless' version '8.1.0'
    id 'io.freefair.lombok' version '9.1.0'
    id 'net.ltgt.errorprone' version '4.3.0'
}

group = 'io.github.bzkf'
version = '3.0.0-beta.171'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', '2025.0.0')
    set('hapiVersion', '8.6.0')
}

dependencies {
    implementation project(':mappings')

    implementation 'org.apache.kafka:kafka-streams'
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka-streams'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:${hapiVersion}"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:${hapiVersion}"
    implementation 'io.micrometer:micrometer-registry-prometheus:1.16.0'
    implementation 'io.micrometer:micrometer-core:1.16.0'
    implementation 'org.miracum:kafka-fhir-serializer:2.0.7'
    implementation 'com.github.slugify:slugify:3.0.7'
    implementation 'io.github.bzkf:obds2-to-obds3:0.1.14'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    // Jackson v3 supports Optional out-of-the-box but isn't yet provided by Spring Boot by default
    // <https://github.com/FasterXML/jackson-modules-java8>
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.fasterxml.jackson.module:jackson-module-jakarta-xmlbind-annotations'
    // https://mvnrepository.com/artifact/io.github.java-diff-utils/java-diff-utils
    implementation 'io.github.java-diff-utils:java-diff-utils:4.16'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation "ca.uhn.hapi.fhir:hapi-fhir-validation:${hapiVersion}"
    testImplementation "ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:${hapiVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-stream-test-support'
    testImplementation 'com.approvaltests:approvaltests:26.2.0'
    testImplementation 'org.apache.kafka:kafka-streams-test-utils'

    errorprone 'com.google.errorprone:error_prone_core:2.45.0'
    errorprone 'com.uber.nullaway:nullaway:0.12.14'

    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'
    runtimeOnly 'org.postgresql:postgresql'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.14'
}

spotless {
    java {
        target('**/*.java')
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}

jacocoTestReport {
    reports {
        csv.required = true
        html.required = true
        xml.required = true
    }
}

import net.ltgt.gradle.errorprone.CheckSeverity

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        check('NullAway', CheckSeverity.WARN)
        option('NullAway:AnnotatedPackages', 'io.github.bzkf.obdstofhir')
    }
    options.errorprone.disableWarningsInGeneratedCode = true
    // ignore legacy mappers code
    // MeldungExportV3 causes an errorprone exception - likely because it can't handle all the lombok annotations
    options.errorprone.errorproneArgs.add('-XepExcludedPaths:(.*/ADT_GEKID\\.java|.*/MeldungExportV3\\.java'
        + '|.*/lookup/.*|.*/mapper/\\w*\\.java)')
}

// via <https://stackoverflow.com/questions/24936781/gradle-plugin-project-version-number>
task printVersion {
    doLast {
        println project.version
    }
}
