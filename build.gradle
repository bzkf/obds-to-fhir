plugins {
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
	id 'java'
	id 'com.diffplug.spotless' version '6.25.0'
	id "io.freefair.lombok" version "8.11"
}

group = 'org.miracum.streams.ume'
version = '2.2.3'
sourceCompatibility = '21'
targetCompatibility = '21'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2024.0.0")
	set('hapiVersion', "7.4.5")
}

dependencies {
	implementation 'org.apache.kafka:kafka-streams'
	implementation 'org.springframework.cloud:spring-cloud-stream'
	implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka-streams'
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:${hapiVersion}"
	implementation 'io.micrometer:micrometer-registry-prometheus:1.14.1'
	implementation 'io.micrometer:micrometer-core:1.14.1'
	implementation "org.miracum:kafka-fhir-serializer:1.0.8"
	implementation "org.apache.commons:commons-text:1.12.0"
	implementation "org.scala-lang:scala-library:2.13.15"

	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
	// Jackson v3 supports Optional out-of-the-box but isn't yet provided by Spring Boot by default
	// <https://github.com/FasterXML/jackson-modules-java8>
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'


	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	testImplementation "ca.uhn.hapi.fhir:hapi-fhir-validation:${hapiVersion}"
	testImplementation "ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:${hapiVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.cloud:spring-cloud-stream-test-support"
	testImplementation "com.approvaltests:approvaltests:24.9.0"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

jacoco {
	toolVersion = "0.8.12"
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
		googleJavaFormat()
	}
}

jacocoTestReport {
	reports {
		csv.required = true
		html.required = true
		xml.required = true
	}
}

jar {
	enabled = false
}
